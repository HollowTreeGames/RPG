title: Oakewood Start
---
<<FinishQuest("Oakewood Greeting")>>
<<SetAvailable("Oakewood Run")>>

<<if IsActive("Oakewood Greeting")>>
    [[Oakewood Greeting]]
<<endif>>

<<if IsActive("Oakewood Stretch Legs")>>
    [[Oakewood Stretch Legs]]
<<endif>>

<<if IsActive("Oakewood Run")>>
    [[Oakewood Run]]
<<endif>>

<<if IsActive("Oakewood Pick Up Rock")>>
	[[Oakewood Pick Up Rock]]
<<endif>>

<<if IsActive("Oakewood Drop Rock")>>
	[[Oakewood Drop Rock]]
<<endif>>
===


title: Oakewood Greeting
---
<<FinishQuest("Oakewood Greeting")>>
Oakewood (Sad): It's about time you woke up.
Belfry:			Can I help with anything?
Oakewood (Sad): Absolutely not. I don't want another forest destroyed.
Belfry (Sad):   But I promise I'll be careful!
Belfry (Sad):   Pleeease? I want to be better!
Oakewood:       ...
Oakewood:       Fine.
<<SetAvailable("Oakewood Stretch Legs")>>
===


title: Oakewood Stretch Legs
---
<<if IsAvailable("Oakewood Stretch Legs")>>
    [[Oakewood Stretch Legs Start]]
<<elseif $oakewood_walking_count < 4>>
    [[Oakewood Stretch Legs Reminder]]
<<else>>
	[[Oakewood Stretch Legs Complete]]
<<endif>>
===
title: Oakewood Stretch Legs Start
---

Belfry:			  	Okay! Tell me what to do!
Oakewood:		  	Erm.
Oakewood (Happy): 	It's been a long journey, so...
Oakewood:		  	Move around a bit. Shake out your body.
<<set $oakewood_walking_count to 0>>
<<StartQuest("Oakewood Stretch Legs")>>
===
title: Oakewood Stretch Legs Reminder
---
Oakewood:		  	Walk around! Get the blood pumping!
===
title: Oakewood Stretch Legs Complete
---
<<FinishQuest("Oakewood Stretch Legs")>>
Belfry (Happy):		How'd I do?
Oakewood:			Yes, that was...
Oakewood (Happy):	Good. Very good.
Oakewood:			You've helped very much.
Oakewood (Sad): 	Now leave me alone.
<<SetAvailable("Oakewood Run")>>
===

title: Oakewood Run
---
<<if IsAvailable("Oakewood Run")>>
    [[Oakewood Run Start]]
<<elseif $oakewood_running_count < 4>>
    [[Oakewood Run Reminder]]
<<else>>
	[[Oakewood Run Complete]]
<<endif>>
===
title: Oakewood Run Start
---
Belfry:				I know you said leave you alone, but something about you makes me want to talk to you again.
Belfry:				Can I do something else to help?
Oakewood:			Urgh!
Oakewood:			Just... do the same thing again, but faster.
<<StartQuest("Oakewood Run")>>
===
title: Oakewood Run Reminder
---
Oakewood:			Pick up the pace!! Make sure you run far away from me!
===
title: Oakewood Run Complete
---
<<FinishQuest("Oakewood Run")>>
Oakewood:			Well done. I had peace and quiet for a whole three seconds. I'm proud of you.
===


title: Oakewood Cycle
---
<<if $oakewood_cycle is 0>>
    Oakewood: Everyone's still mad at you, you know.
<<elseif $oakewood_cycle is 1>>
    Oakewood: You've been known to screw things up in the past, Belfry, but this time just takes the cake.
<<elseif $oakewood_cycle is 2>>
    Oakewood (Happy): *sigh*
<<elseif $oakewood_cycle is 3>>
    Oakewood: Why are you still talking to me?
<<elseif $oakewood_cycle is 4>>
    Oakewood (Happy): I'm sure that big brute of a sheriff has something to say to you.
<<endif>>

<<set $oakewood_cycle to $oakewood_cycle + 1>>
<<if $oakewood_cycle > 4>>
    <<set $oakewood_cycle to 0>>
<<endif>>
===