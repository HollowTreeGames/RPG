title: Henry Start
---
<<if IsActive("Henry Greeting")>>
    [[Henry Greeting]]
<<endif>>

<<if IsActive("Henry Dank Herb")>>
    [[Henry Dank Herb]]
<<endif>>

<<if IsActive("Henry Dank Book")>>
    [[Henry Dank Book]]
<<endif>>

[[Henry Thanks]]
===

title: Henry Greeting
---
Henry:          Hi! My name is Henry! I'm the sheriff!
Belfry (happy): I know who you are, Henry! We lived in Treehollow together!
Henry (happy):  Of course!
Henry:          But it's my duty as sheriff to greet everyone!
Henry:          Allow me to finish my introduction. Ahem.
Henry (happy):  I like sniffing butts!
Henry (sad):    Nora likes it when I don't wear pants, but I don't know why!
Belfry (sad):   ...
<<FinishQuest("Henry Greeting")>>
===


title: Henry Dank Herb
---
<<if IsAvailable("Henry Dank Herb")>>
    [[Henry Dank Herb Start]]
<<elseif IsInProgress("Henry Dank Herb")>>
    <<if GetInventory() == "Dank Herb">>
        [[Henry Dank Herb Get]]
    <<else>>
        [[Henry Dank Herb Reminder]]
    <<endif>>
<<endif>>
===
title: Henry Dank Herb Start
---
Henry (Sad):    Nora has been really stressed by her work lately. You know what she needs?
Belfry:         A nice back massage?
Henry:          That's right! A little bit of kibbles and hits!
Belfry (Sad):   ...
Henry (Happy):  Would you please find me some nutritious nug to help Nora take the edge off?
Belfry (Happy): Anything for you guys!
<<StartQuest("Henry Dank Herb")>>
===
title: Henry Dank Herb Reminder
---
Henry:          Have you found some leafs of the devil's lettuce?
Belfry:         Working on it!
===
title: Henry Dank Herb Get
---
<<ClearInventory()>>
Henry (Happy):  Wow, thank you! Nora will be so happy to smoke this straight killer kush!
<<FinishQuest("Henry Dank Herb")>>
===


title: Henry Dank Book
---
<<if IsAvailable("Henry Dank Book")>>
    [[Henry Dank Book Start]]
<<elseif IsInProgress("Henry Dank Book")>>
    <<if GetInventory() == "Herb Book">>
        [[Henry Dank Book Get]]
    <<else>>
        [[Henry Dank Book Reminder]]
    <<endif>>
<<endif>>
===
title: Henry Dank Book Start
---
Henry (Happy):  So I wanted to get Nora a special book...
Henry:          You didn't hear this from me, but it's a book about...
Henry (Sad):    WEED.
Belfry (Sad):   ...ah.
Henry:          Can you find one for me? She's hopeless at blazing 420 365 blaze it.
Belfry (Happy): We can't have that, now can we? I'll look.
<<StartQuest("Henry Dank Book")>>
===
title: Henry Dank Book Reminder
---
Henry:          Have you found that book yet?
Henry:          Keep it on the down-low.
Henry (Sad):    I'm a sheriff, you know.
===
title: Henry Dank Book Get
---
<<ClearInventory()>>
Henry (Happy):  Thank goodness! I was worried Nora would choke on a blunt without this.
<<FinishQuest("Henry Dank Book")>>
===


title: Henry Thanks
---
Henry:          Thanks again for the dank dire doobies and this mad awesome book!
Henry (Sad):    Nora asked me to stop saying dank, but it's too much fun!
Henry (Happy):  Dank dank dank dank dank dank dank dank dank dank dank dank dank dank dank dank dank dank dank dank dank!
===